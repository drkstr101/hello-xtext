grammar drkstr.hello.xtext.natural.Natural hidden(WS)
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate natural "http://www.agileware.org/natural/core"

Model:
	scenarios+=Scenario*
;
	
Scenario: {Scenario}
	'Scenario:' title=Text? EOL
	steps+=Step*
;

Step:
	keyword=STEP_KEYWORD
	description=Text
	EOL
;

STEP_KEYWORD: ANY_KEYWORD
	| GIVEN_KEYWORD
	| WHEN_KEYWORD
	| THEN_KEYWORD
	| AND_KEYWORD
;

terminal ANY_KEYWORD: '*';
terminal GIVEN_KEYWORD: 'Given';
terminal WHEN_KEYWORD: 'When';
terminal THEN_KEYWORD: 'Then';
terminal AND_KEYWORD: 'And';

Text returns ecore::EString:
 	TextLiteral+
;

TextLiteral: PLACEHOLDER
	| NUMBER
	| STRING
	| WORD
	| ANY_OTHER
;

// ----------------------------------------------------------
//
// Common Terminals
//
// ----------------------------------------------------------

terminal PLACEHOLDER:
	('<' WORD '>')
	| ('[' WORD ']')
;

terminal WORD: 
	(LETTER | DIGIT)
	(LETTER | DIGIT | '_' | '-' | '.' )*
;

terminal NUMBER returns ecore::EInt:
    '-'? ('0'..'9')+ ('.' ('0'..'9')+)?
;
 
terminal STRING returns ecore::EString:
    '"' ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' 
	    | "'" ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
;

terminal EOL: ('\r'? '\n')+;

terminal WS: (SPACE | '\t');

terminal ANY_OTHER: .;



// ----------------------------------------------------------
//
// Unicode Character Ranges
//
// ----------------------------------------------------------

terminal fragment PUNCTUATION: ASCII_SYMBOLS;

terminal fragment SPACE: ASCII_SPACE;

terminal fragment DIGIT: ASCII_DIGIT;

terminal fragment LETTER: LATIN_ALPHABET;

// Basic Latin
////

terminal fragment ASCII_SPACE: '\u0020';

terminal fragment ASCII_DIGIT: ('\u0030'..'\u0039');

// all ascii chars other than letters, digits, space, or control codes
terminal fragment ASCII_SYMBOLS: ('\u0021'..'\u002F')
	| ('\u003A'..'\u0040')
	| ('\u005B'..'\u0060')
	| ('\u007B'..'\u007E')
;

terminal fragment LATIN_ALPHABET: ('\u0041'..'\u005A')
	| ('\u0061'..'\u007A')
;
